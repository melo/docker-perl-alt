#!/usr/bin/env perl

use strict;
use warnings;
use Scalar::Util ();
use autodie;
use Getopt::Long;
use Path::Tiny;
use Digest::SHA;

sub usage {
  die "Usage: $0 [--push] [--filter=s] [--check]\n";
}

my %cfg = (repo => 'melopt/perl-alt');
GetOptions(\%cfg, 'help|?', 'push', 'filter=s', 'repo=s', 'multiplatform', 'check', 'debug') or usage();
usage() if $cfg{help};

my $repo = $cfg{repo};

my @versions = (
  ['perl',       'latest', '5.40-slim', 1],
  ['perl',       'full',   '5.40'],
  ['alpine',     'latest', '3.20'],
  ['alpine',     'next',   'edge'],
  ['alpine',     'edge',   'edge'],
  ['chainguard', 'latest', 'latest', undef, 'cgr.dev/chainguard/wolfi-base'],
);

## Lambda
my %lambda_runtime_versions = (
  aarch64 => [
    'https://github.com/aws/aws-lambda-runtime-interface-emulator/releases/download/v1.22/aws-lambda-rie-arm64',
    '15ae5356c8e76cc483007c7cfe1aa821cae10afd259eab82ad8b7102ff486fdf',
  ],
  x86_64 => [
    'https://github.com/aws/aws-lambda-runtime-interface-emulator/releases/download/v1.22/aws-lambda-rie-x86_64',
    '9badeae7e0ed0667afa60f79fd250002dfae38104f23ebc80508e247616ac97a',
  ],
);
if ($cfg{check}) {
  for my $arch (sort keys %lambda_runtime_versions) {
    my ($url, $wanted) = $lambda_runtime_versions{$arch}->@*;

    my $tmp = Path::Tiny->tempfile;
    system('/usr/bin/curl', '-sLo', $tmp, $url);

    my $d = Digest::SHA->new(256);
    $d->addfile($tmp->stringify);
    my $got = $d->hexdigest;

    my $status = $wanted eq $got ? 'ok' : 'out-of-date';
    print "$status: $arch $url\n\twanted $wanted\n\tactual $got\n\tfile $tmp";
  }
  exit(0);
}

## Multiplatform support
my @plats;
my $docker_cmd = 'docker';
if ($cfg{multiplatform}) {
  $docker_cmd = 'nerdctl';
  push @plats, '--platform', 'linux/arm64,linux/amd64';
  print ">>>>>>>>>>> MULTIPLATFORM Build detected: @plats\n";
  sleep(5);
}

for my $spec (@versions) {
  my ($f, $t, $v, $x, $bi) = @$spec;
  $bi = $f unless $bi;

  my $baset = "$repo:$f-$t";
  my $basev = "$repo:$f-$v";


  my @tags;
  for my $target (qw( devel build runtime reply )) {
    my $tagt = "$baset-$target";
    my $tagv = "$basev-$target";
    my $tagx = $x                         ? "$target" : "";
    my $tagl = $x && $target eq 'runtime' ? "latest"  : "";

    if ($cfg{filter} and $tagt !~ m/$cfg{filter}/) {
      print ">>>>>>>> Skipped via filter '$cfg{filter}'\n";
      next;
    }

    print ">>>> target $target, base $v: $tagt\n";
    print ">>>> target $target, base $v: $tagv\n";
    print ">>>> also tagged: $tagx\n" if $tagx;
    print ">>>> also tagged: $tagl\n" if $tagl;
    print "\n";

    my @itags = ($tagt, $tagv);
    push @itags, $tagx  if $tagx;
    push @itags, $tagl  if $tagl;
    push @itags, $baset if $target eq "devel";

    my @cmd = ($docker_cmd, 'build');
    push @cmd, '--progress', 'plain' if $cfg{debug};
    push @cmd, @plats if @plats;
    push @cmd, '--target' => $target;
    push @cmd, '--file'   => "Dockerfile.$f";
    push @cmd, map { ('--tag', $_) } @itags;
    push @cmd, '--label' => 'maintainer=Pedro Melo <melo@simplicidade.org>';
    push @cmd,
      '--build-arg' => "BASE=$bi:$v",
      '--build-arg' => "AWS_LAMBDA_RIE_URL_aarch64=$lambda_runtime_versions{aarch64}[0]",
      '--build-arg' => "AWS_LAMBDA_RIE_SIG_aarch64=$lambda_runtime_versions{aarch64}[1]",
      '--build-arg' => "AWS_LAMBDA_RIE_URL_x86_64=$lambda_runtime_versions{x86_64}[0]",
      '--build-arg' => "AWS_LAMBDA_RIE_SIG_x86_64=$lambda_runtime_versions{x86_64}[1]";
    push @cmd, '.';

    my $err = my_system(@cmd);
    die "FATAL: failed to build Docker image for target $target, base $v: $tagt\n" if $err;

    push @tags, @itags;

    if ($cfg{push}) {
      my_system($docker_cmd, 'push', @plats, $_) for @tags;
      @tags = ();
    }
  }
}


print "\n\n>>> DONE\n\n";

sub my_system {
  print "\n\n>>>>>>>> Cmd: @_\n\n";
  return system(@_);
}
