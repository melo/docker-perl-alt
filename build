#!/usr/bin/env perl

use strict;
use warnings;
use autodie;
use Getopt::Long;

sub usage {
  die "Usage: $0 [--push] [--filter=s]\n";
}

my %cfg = ( repo => 'melopt/perl-alt' );
GetOptions(\%cfg, 'help|?', 'push', 'filter=s', 'repo=s') or usage();
usage() if $cfg{help};

my $repo = $cfg{repo};

my @versions = (
  ['alpine', 'latest', '3.9'], ['alpine', 'next', '3.16'], ['alpine', 'edge', 'edge'],
  ['perl',   'latest', '5.36-slim'], ['perl',   'full', '5.36'], 
);

my @tags;
for my $spec (@versions) {
  my ($f, $t, $v) = @$spec;
  my $baset = "$repo:$f-$t";
  my $basev = "$repo:$f-$v";

  for my $target (qw( devel build runtime )) {
    my $tagt = "$baset-$target";
    my $tagv = "$basev-$target";

    next if $cfg{filter} and $tagt !~ m/$cfg{filter}/;

    print ">>>> target $target, base $v: $tagt\n";
    print ">>>> target $target, base $v: $tagv\n\n";

    system(qw(docker build --target), $target, '--file', "Dockerfile.$f", '--tag', $tagt, '--tag', $tagv, '--build-arg', "BASE=$f:$v", '.');
    push @tags, $tagt, $tagv;
  }

  system(qw(docker tag), "$baset-devel", $baset);
  push @tags, $baset;
}

if ($cfg{push}) {
  system('docker', 'push', $_) for @tags;
}
