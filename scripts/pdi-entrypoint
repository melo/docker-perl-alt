#!/usr/bin/env perl
#
# Default entrypoint script
#

use strict;
use warnings;
use autodie;
use Path::Tiny;
use Config;

## Update the CPAN deps if PDI_UPDATE_DEPS is present
system('/usr/bin/pdi-build-deps') if $ENV{PDI_UPDATE_DEPS};

## Collect folders for PERL5LIB
my @incs = ('/app/lib');

## Search for submodules
my $base = path('/app/elib');
if ($base->is_dir) {
  for my $sm ($base->children) {
    my $sml = $sm->child('lib');
    next unless $sml->is_dir;

    push @incs, $sml->stringify;
  }
}

## Add deps
for my $d ('/deps', '/stack') {
  for my $s ('lib', 'local/lib/perl5', "local/lib/perl5/$Config{'archname'}") {
    push @incs, "$d/$s" if -d "$d/$s";
  }
}

## Generate final PERL5LIB
$ENV{PERL5LIB} = join(':', grep { $_ } (@incs, $ENV{PERL5LIB}));

## Per project entrypoint support :)
my @entrypoint;
push @entrypoint, '/entrypoint' if -x '/entrypoint';

## Exec the command
exec(@entrypoint, @ARGV) if @ARGV;
for ('/bin/bash', '/bin/sh') { exec(@entrypoint, $_) if -x }
die "FATAL: could not find any shell!!!";
